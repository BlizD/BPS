Процедура ОтправитьНаСогласование(ДопПараметры) Экспорт
	ОбъектБД = ДопПараметры.ОбъектБД;
	ПредметСогласования = ДопПараметры.ПредметСогласования;
	Согласование = ДопПараметры.Согласование;
	
	
	ТребуетсяСогласование = ПолучитьТребуетсяЛиСогласование(ДопПараметры);
	Если НЕ ТребуетсяСогласование Тогда
		ВызватьИсключение "Ошибка! нет алгоритма, если не требуется согласование";
	Иначе
		Если ЗначениеЗаполнено(Согласование) Тогда			
			Если Согласование.Стартован <> Согласование.Завершен Тогда
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Ошибка! Уже есть запущенный бизнес процесс ""Согласование"", для этого объекта. "
					+"Используйте кнопку ""Открыть процесс согласования"" для его просмотра";
				Сообщение.Сообщить();
				Возврат;
			ИначеЕсли Согласование.Завершен Тогда
				ПроцессОбъект = БизнесПроцессы.бпсСогласование.СоздатьБизнесПроцесс();
			ИначеЕсли НЕ Согласование.Стартован Тогда
				ПроцессОбъект = Согласование.ПолучитьОбъект();						
			Иначе
				ВызватьИсключение "Ошибка при отправке на утверждение";
			Конецесли;
		Иначе
			ПроцессОбъект = БизнесПроцессы.бпсСогласование.СоздатьБизнесПроцесс();	
		Конецесли;
		
		ПроцессОбъект.Дата = ТекущаяДата();
		
		ВТДопПараметры = Новый Структура();
		ВТДопПараметры.Вставить("ОбъектБД",ОбъектБД);
		ВТДопПараметры.Вставить("ПредметСогласования",ПредметСогласования);			
		ПроцессОбъект.Заполнить(ВТДопПараметры);
		
		ПроцессОбъект.Записать();
		ПроцессОбъект.Старт();
	Конецесли;
	
КонецПроцедуры 

Процедура ОтменитьСогласование(ДопПараметры) Экспорт
	ОбъектБД = ДопПараметры.ОбъектБД;
	ПредметСогласования = ДопПараметры.ПредметСогласования;
	Согласование = ДопПараметры.Согласование;
		
	ТЗНеВыполненныеЗадачи = ПолучитьТЗНеВыполненныеЗадачи(ДопПараметры);	
	Если ТЗНеВыполненныеЗадачи.Количество() = 0 Тогда
		Статус = ПредопределенноеЗначение("Справочник.бпсСтатусыОбъектов.Согласование_Отклонено");
		ВТДопПараметры = Новый Структура();
		ВТДопПараметры.Вставить("ОбъектБД",ОбъектБД);
		ВТДопПараметры.Вставить("ПредметСогласования",ПредметСогласования);
		ВТДопПараметры.Вставить("Статус",Статус);		
		УстановитьСтатус(ВТДопПараметры);
		
		Сообщить("Отменено согласование для "
			+" ОбъектБД ["+ОбъектБД+"] "
			+" ПредметСогласования ["+ПредметСогласования+"]");			
		Возврат;
	Конецесли;
	
	//Если есть не выполненные задачи по запущенному согласованию
	Для каждого СтрокаТЗНеВыполненныеЗадачи из ТЗНеВыполненныеЗадачи цикл
		ЗадачаСсылка = СтрокаТЗНеВыполненныеЗадачи.ЗадачаСсылка;
		Если ЗадачаСсылка.Выполнена Тогда
			Продолжить;
		Конецесли;
		БизнесПроцесс = ЗадачаСсылка.БизнесПроцесс;		
		Если ТипЗнч(БизнесПроцесс) = Тип("БизнесПроцессСсылка.бпсСогласование") Тогда	
			
			БизнесПроцессОбъект = БизнесПроцесс.ПолучитьОбъект();
			БизнесПроцессОбъект.РазрешеноПовторноеСогласование = Ложь;
			
			ТекстОтменыЗаявки = "Отменено пользователем " + ПараметрыСеанса.ТекущийПользователь;
			
			КомандаСогласования = ПредопределенноеЗначение("Перечисление.бпсКомандыСогласования.НеСогласовано");
			ВТДопПараметрыОтмены = Новый Структура();
			ВТДопПараметрыОтмены.Вставить("ЗадачаСсылка",ЗадачаСсылка);				
			ВТДопПараметрыОтмены.Вставить("Рецензия",ТекстОтменыЗаявки);	
			ВТДопПараметрыОтмены.Вставить("КомандаСогласования",КомандаСогласования);
			
			НачатьТранзакцию();
			
			Попытка
				БизнесПроцессОбъект.Записать();
			Исключение
				ВызватьИсключение "Ошибка! Не удалось записать БП: "+БизнесПроцесс 
				+" Описание ошибки ["+ОписаниеОшибки()+"]";
			КонецПопытки;
			
			Попытка
				БизнесПроцессОбъект.ВыполнитьЗадачу(ВТДопПараметрыОтмены);
			Исключение
				ВызватьИсключение "Ошибка! Не удалось отменить задачу: "+ЗадачаСсылка+" БП: "+БизнесПроцесс 
				+" Описание ошибки ["+ОписаниеОшибки()+"]";
			КонецПопытки;
			
			ЗафиксироватьТранзакцию();
		Иначе
			ВызватьИсключение "Ошибка! Не удалось отменить задачу: "+ЗадачаСсылка+" БП: "+БизнесПроцесс;
		Конецесли;	
	Конеццикла;
	
	Сообщить("Отменено согласование для "
		+" ОбъектБД ["+ОбъектБД+"] "
		+" ПредметСогласования ["+ПредметСогласования+"]");	
	
КонецПроцедуры 

Процедура УстановитьСтатусВручную(ДопПараметры) Экспорт
	ОбъектБД = ДопПараметры.ОбъектБД;
	ПредметСогласования = ДопПараметры.ПредметСогласования;
	Согласование = ДопПараметры.Согласование;
	пСтатус = ДопПараметры.Статус;
		
	ТЗНеВыполненныеЗадачи = ПолучитьТЗНеВыполненныеЗадачи(ДопПараметры);	
	Если ТЗНеВыполненныеЗадачи.Количество() > 0 Тогда
		Сообщить("Ошибка! нельзя установить статус вручную, т.к. еще идет согласование. "
			+ "Необходимо или дождаться окончания согласования или отменить согласование. Затем повторить попытку");
		Возврат;
	Конецесли;	
	
	ВТДопПараметры = Новый Структура();
	ВТДопПараметры.Вставить("ОбъектБД",ОбъектБД);
	ВТДопПараметры.Вставить("ПредметСогласования",ПредметСогласования);
	ВТДопПараметры.Вставить("Статус",пСтатус);		
	УстановитьСтатус(ВТДопПараметры);
	
	Сообщить("Установлен статус ["+пСтатус+"] для "
		+" ОбъектБД ["+ОбъектБД+"] "
		+" ПредметСогласования ["+ПредметСогласования+"]");			
КонецПроцедуры 

Процедура УстановитьСтатус(ВТДопПараметры)
	ДопПараметры = Новый Структура();
	ДопПараметры.Вставить("ДатаИзмененияСтатуса",ТекущаяДата());
	ДопПараметры.Вставить("ПредметСогласования",ВТДопПараметры.ПредметСогласования);
	ДопПараметры.Вставить("Статус",ВТДопПараметры.Статус);	
	ДопПараметры.Вставить("Комментарий","Отменено пользователем: " + ПараметрыСеанса.ТекущийПользователь);
	
	ТЗОбъектыБД = РегистрыСведений.бпсСтатусыОбъектов.ПолучитьОписаниеТЗОбъектыБД();
	
	СтрокаТЗОбъектыБД = ТЗОбъектыБД.Добавить();
	СтрокаТЗОбъектыБД.ОбъектБД = ВТДопПараметры.ОбъектБД;
	СтрокаТЗОбъектыБД.ПредметСогласования = ВТДопПараметры.ПредметСогласования;
	СтрокаТЗОбъектыБД.Статус = ВТДопПараметры.Статус;
	
	ДопПараметры.Вставить("ТЗОбъектыБД",ТЗОбъектыБД);
	
	Документы.бпсРегистрацияСтатусаОбъекта.УстановитьСтатусыОбъектов(ДопПараметры);	
КонецПроцедуры 



Функция ПолучитьТЗНеВыполненныеЗадачи(ДопПараметры) Экспорт 
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Задача.Ссылка КАК ЗадачаСсылка,
	|	Задача.Пользователь,
	|	Задача.РольАдресации,
	|	Задача.ПодразделениеАдресации
	|ИЗ
	|	Задача.бпсЗадача КАК Задача
	|ГДЕ
	|	НЕ Задача.Выполнена
	|	И Задача.БизнесПроцесс ССЫЛКА БизнесПроцесс.бпсСогласование
	|	И ВЫРАЗИТЬ(Задача.БизнесПроцесс КАК БизнесПроцесс.бпсСогласование).ОбъектБД = &ОбъектБД
	|	И ВЫРАЗИТЬ(Задача.БизнесПроцесс КАК БизнесПроцесс.бпсСогласование).ПредметСогласования = &ПредметСогласования
	|");
	
	Запрос.УстановитьПараметр("ОбъектБД", ДопПараметры.ОбъектБД);
	Запрос.УстановитьПараметр("ПредметСогласования", ДопПараметры.ПредметСогласования);

	Возврат Запрос.Выполнить().Выгрузить(); 
КонецФункции //ПолучитьТЗНеВыполненныеЗадачи()

Функция ПолучитьТребуетсяЛиСогласование(ДопПараметры) 
	пТребуетсяЛиСогласование = Истина;
	Возврат пТребуетсяЛиСогласование;
КонецФункции 