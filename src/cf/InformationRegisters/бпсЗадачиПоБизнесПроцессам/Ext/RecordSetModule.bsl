Процедура СформироватьЗаписи(ДопПараметры,Отказ) Экспорт
	Если НЕ ДопПараметры.Свойство("ЗадачаСсылка") Тогда 
		Сообщить("Ошибка! Не все параметры заданы для формирования записей в РС: бпсЗадачиПоБизнесПроцессам");
		Отказ = Истина;
		Возврат;
	Конецесли;
	ЗадачаСсылка = ДопПараметры.ЗадачаСсылка;
	ОбъектБД = Неопределено;
	Если ДопПараметры.Свойство("ОбъектБД") Тогда
		ОбъектБД = ДопПараметры.ОбъектБД;
	Конецесли;
	
	ИсходныйБизнесПроцесс = ПолучитьИсходныйБППоЗадаче(ЗадачаСсылка);
	РезультатФункции = ПолучитьОбъектБДПоИсходномуБП(ИсходныйБизнесПроцесс);
	Если РезультатФункции.НеФормироватьЗаписи Тогда
		Возврат;
	Конецесли;
	
	Если НЕ ЗначениеЗаполнено(ОбъектБД) Тогда
		ОбъектБД = РезультатФункции.ОбъектБД;
	Конецесли;
	
	Если НЕ ЗначениеЗаполнено(ОбъектБД) Тогда
		Отказ = Истина;
		Возврат;
	Конецесли;
	
	ПараметрыДляЗаписи = ПолучитьПараметрыДляЗаписи(ЗадачаСсылка);
	
	НаборЗаписей=РегистрыСведений.бпсЗадачиПоБизнесПроцессам.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ОбъектБД.Установить(ОбъектБД);
	НаборЗаписей.Отбор.ИсходныйБизнесПроцесс.Установить(ИсходныйБизнесПроцесс);
	НаборЗаписей.Отбор.Задача.Установить(ЗадачаСсылка);
	НаборЗаписей.Прочитать();               
	НаборЗаписей.Очистить();
	
	Запись = НаборЗаписей.Добавить();
	Запись.Активность = Истина;
	Запись.ОбъектБД = ОбъектБД;
	Запись.ИсходныйБизнесПроцесс = ИсходныйБизнесПроцесс;
	Запись.Задача = ЗадачаСсылка;
	
	БизнесПроцесс = ЗадачаСсылка.БизнесПроцесс;
	Запись.БизнесПроцесс = БизнесПроцесс;
	Запись.ДатаБизнесПроцесса = БизнесПроцесс.Дата;
	Запись.ПредметСогласования = БизнесПроцесс.ПредметСогласования;
	
	Если ДопПараметры.Свойство("Рецензия") Тогда
		Запись.Рецензия = ДопПараметры.Рецензия;
	Конецесли;
	Если ДопПараметры.Свойство("Согласовано") Тогда
		Запись.Согласовано = ДопПараметры.Согласовано;
	Конецесли;
	Если ДопПараметры.Свойство("Очередь") Тогда
		Запись.Очередь = ДопПараметры.Очередь;
	Конецесли;
	Запись.ВыводитьВОтчет = ПараметрыДляЗаписи.ВыводитьВОтчет;
	НаборЗаписей.Записать();

КонецПроцедуры //СформироватьЗаписи(ЗадачаСсылка)

Функция ПолучитьИсходныйБППоЗадаче(ЗадачаСсылка) 
	ИсходныйБП = ЗадачаСсылка.БизнесПроцесс;
	ВедущаяЗадачаИсходногоБП = ИсходныйБП.ВедущаяЗадача;
	Если ЗначениеЗаполнено(ВедущаяЗадачаИсходногоБП) Тогда
		ИсходныйБП = ПолучитьИсходныйБППоЗадаче(ВедущаяЗадачаИсходногоБП);
	Конецесли;
	Возврат ИсходныйБП;
КонецФункции //ПолучитьИсходныйБППоЗадаче(ЗадачаСсылка)

Функция ПолучитьОбъектБДПоИсходномуБП(ИсходныйБизнесПроцесс) 
	ОбъектБД = Неопределено;
	РезультатФункции = Новый Структура();
	РезультатФункции.Вставить("ОбъектБД",ОбъектБД);
	РезультатФункции.Вставить("НеФормироватьЗаписи",Ложь);
	
	ТипЗнчИсходныйБизнесПроцесс = ТипЗнч(ИсходныйБизнесПроцесс);	
	ОбъектБД = ИсходныйБизнесПроцесс.ОбъектБД;		
	РезультатФункции.Вставить("ОбъектБД",ОбъектБД);
	Возврат РезультатФункции;
КонецФункции //

Функция ПолучитьПараметрыДляЗаписи(ЗадачаСсылка)
	ВыводитьВОтчет = Истина;
	Если ЗадачаСсылка.ТочкаМаршрута = ПредопределенноеЗначение("БизнесПроцесс.бпсСогласование.ТочкаМаршрута.ОбработатьРезультатыСогласования") Тогда
		ВыводитьВОтчет = Ложь;
	КонецЕсли;
	ПараметрыДляЗаписи = Новый Структура();
	ПараметрыДляЗаписи.Вставить("ВыводитьВОтчет",ВыводитьВОтчет);	
	Возврат ПараметрыДляЗаписи;
КонецФункции //ПолучитьЭтоСлужебнаяЗадача(ЗадачаСсылка)