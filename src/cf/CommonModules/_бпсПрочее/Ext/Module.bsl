Процедура УстановитьЗначениеПараметровСеанса() Экспорт
	//ВызватьИсключение "УстановитьЗначениеПараметровСеанса()"; 
	ПараметрыСеанса.ТекущийПользователь = ПолучитьТекущегоПользователя(); 	
КонецПроцедуры //УстановитьЗначениеПараметровСеанса

Функция ПолучитьТекущегоПользователя()
	пТекущийПользователь = Неопределено;
	Попытка
		пТекущийПользователь = Вычислить("ПараметрыСеанса.ТекущийПользователь");
	Исключение
	КонецПопытки;
	
	Если ЗначениеЗаполнено(пТекущийПользователь) Тогда
		Возврат пТекущийПользователь;
	Конецесли;
	
	
	Если ПустаяСтрока(ИмяПользователя()) Тогда
		ИмяПользователя           = "<Не указан>";
		ПолноеИмяПользователя     = "<Не указан>";
	Иначе
		ИмяПользователя           = ИмяПользователя();
		
		Если ПустаяСтрока(ПолноеИмяПользователя()) Тогда
			ПолноеИмяПользователя = ИмяПользователя;
		Иначе
			ПолноеИмяПользователя = ПолноеИмяПользователя();
		КонецЕсли;
	КонецЕсли;
	
	ДлинаКодаПользователя = Метаданные.Справочники.Пользователи.ДлинаКода;
	
	Если СтрДлина(ИмяПользователя) > ДлинаКодаПользователя Тогда
		ИмяПользователя = Лев(ИмяПользователя, ДлинаКодаПользователя);
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Пользователи.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|ГДЕ
	|	(НЕ Пользователи.ЭтоГруппа)
	|	И Пользователи.Код = &Код";
	
	Запрос.УстановитьПараметр("Код", ИмяПользователя);
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		
		ОбъектПользователь = Справочники.Пользователи.СоздатьЭлемент();
		
		ОбъектПользователь.Код          = ИмяПользователя;
		ОбъектПользователь.Наименование = ПолноеИмяПользователя;
	 
		ОбъектПользователь.УстановитьСсылкуНового(Справочники.Пользователи.ПолучитьСсылку());
		пТекущийПользователь = ОбъектПользователь.Ссылка;

		Попытка
			ОбъектПользователь.Записать();		
		Исключение
			#Если ТолстыйКлиентОбычноеПриложение Тогда
			Предупреждение("Пользователь : " + ИмяПользователя + " не был найден в справочнике пользователей. Возникла ошибка при добавлении пользователя в справочник.
				|" + ОписаниеОшибки());
			ЗавершитьРаботуСистемы(Ложь);
			#КонецЕсли
			Возврат Справочники.Пользователи.ПустаяСсылка();
		КонецПопытки;

		пТекущийПользователь = ОбъектПользователь.Ссылка;

	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();

		пТекущийПользователь = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат пТекущийПользователь;
КонецФункции
