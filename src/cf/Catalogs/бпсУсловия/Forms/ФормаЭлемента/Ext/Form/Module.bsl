
&НаСервере
Функция ОпределитьОтборНаСервере(пНастройкиКомпоновщика = Неопределено) Экспорт
	МакетСКД = Справочники.бпсУсловия.ПолучитьМакетСКДДляТипаОбъектаБД(Объект.Владелец);
	
	АдресВременногоХранилищаМакетаСКД = ПоместитьВоВременноеХранилище(МакетСКД,Новый УникальныйИдентификатор());
	
	ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресВременногоХранилищаМакетаСКД);
	КомпоновщикНастроекКомпоновкиДанных.Инициализировать(ИсточникНастроек);
	
	Если пНастройкиКомпоновщика = Неопределено Тогда
		КомпоновщикНастроекКомпоновкиДанных.ЗагрузитьНастройки(МакетСКД.НастройкиПоУмолчанию);
	Иначе
		КомпоновщикНастроекКомпоновкиДанных.ЗагрузитьНастройки(пНастройкиКомпоновщика);
	Конецесли;
	//ЗначениеВРеквизитФормы(КомпоновщикНастроекКомпоновкиДанных,"КомпоновщикНастроекКомпоновкиДанных");
	//ЗначениеВРеквизитФормы(СправочникОбъект,"Объект");
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Объект.Владелец) Тогда
		СправочникОбъект = РеквизитФормыВЗначение("Объект");
		пНастройкиКомпоновщика = СправочникОбъект.НастройкиКомпоновщикаХранилище.Получить();
		ОпределитьОтборНаСервере(пНастройкиКомпоновщика);
	Конецесли;
	УстановитьВидимостьДоступность();
КонецПроцедуры


&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
Если Объект.ВыборПоПодразделениям И СокрЛП(Объект.РеквизитПодразделение) = "" Тогда
	Отказ = Истина;
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = НСтр("ru = 'Установлен флаг выбора по подразделениям, нужно указать реквизит Подразделение'");
	Сообщение.Поле = "Объект.РеквизитПодразделение";
	Сообщение.УстановитьДанные(Объект);
	Сообщение.Сообщить();
Иначе
	Объект.АдресВременногоХранилища = ПоместитьВоВременноеХранилище(КомпоновщикНастроекКомпоновкиДанных.Настройки,Новый УникальныйИдентификатор());	
КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоступность() Экспорт
	Элементы.ГруппаСтраницаОтборы.Видимость = Ложь;
	//Элементы.ГруппаСтраницаТекстЗапроса.Видимость = Ложь;
	Элементы.ГруппаТекстПроизвольногоУсловия.Видимость = Ложь;
	
	Если Объект.ВыборПоПодразделениям Тогда
		Элементы.РеквизитПодразделение.Видимость = Истина;
	Иначе
		Элементы.РеквизитПодразделение.Видимость = Ложь;
	КонецЕсли;
	
	Если Объект.ПроизвольноеУсловие Тогда
		Элементы.ГруппаТекстПроизвольногоУсловия.Видимость = Истина;
	Иначе
		Элементы.ГруппаСтраницаОтборы.Видимость = Истина;
		//Элементы.ГруппаСтраницаТекстЗапроса.Видимость = Истина;
	Конецесли;
		
	
КонецПроцедуры //УстановитьВидимостьДоступность()

&НаКлиенте
Процедура КомпоновщикНастроекКомпоновкиДанныхНастройкиОтборПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	СформироватьНаименование();
КонецПроцедуры


&НаКлиенте
Процедура ПроизвольноеУсловиеПриИзменении(Элемент)
	УстановитьВидимостьДоступность();
КонецПроцедуры


&НаКлиенте
Процедура КомпоновщикНастроекКомпоновкиДанныхНастройкиОтборПриИзменении(Элемент)
	СформироватьНаименование();
КонецПроцедуры

&НаКлиенте
Процедура СформироватьНаименование()
	Объект.Наименование = "" + КомпоновщикНастроекКомпоновкиДанных.Настройки.Отбор;
КонецПроцедуры //СформироватьНаименование()


&НаКлиенте
Процедура ВладелецПриИзменении(Элемент)
	ОпределитьОтборНаСервере();
	РеквизитПодразделениеЗаполнитьСписокВыбора();
КонецПроцедуры


&НаКлиенте
Процедура ВыборПоПодразделениямПриИзменении(Элемент)
	
	Если Объект.ВыборПоПодразделениям Тогда
		Если СокрЛП(Объект.Наименование) = "" Тогда
			Объект.Наименование = "<Выбор по подразделениям>";
		КонецЕсли;

		РеквизитПодразделениеЗаполнитьСписокВыбора();
	КонецЕсли;
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры


&НаСервереБезКонтекста
Функция ВернутьРеквизитыОбъектаМетаданных(типОбъектаБД)
	ОбъектМетаданные = Метаданные[ТипОбъектаБД.МенеджерОбъекта][ТипОбъектаБД.ОбъектБДИмяМетаданных];
	
	РеквизитыОбъектаМетаданных = Новый Массив;
	
	Реквизиты = ОбъектМетаданные.Реквизиты;
	Для Каждого Реквизит Из Реквизиты Цикл
		РеквизитыОбъектаМетаданных.Добавить(Реквизит.Имя);
	КонецЦикла;
	Возврат РеквизитыОбъектаМетаданных;
КонецФункции

&НаКлиенте
Процедура РеквизитПодразделениеЗаполнитьСписокВыбора()
	Элементы.РеквизитПодразделение.СписокВыбора.Очистить();
	Если Объект.ВыборПоПодразделениям И ЗначениеЗаполнено(Объект.Владелец) Тогда
		Элементы.РеквизитПодразделение.СписокВыбора.ЗагрузитьЗначения(ВернутьРеквизитыОбъектаМетаданных(Объект.Владелец));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	РеквизитПодразделениеЗаполнитьСписокВыбора();
КонецПроцедуры

