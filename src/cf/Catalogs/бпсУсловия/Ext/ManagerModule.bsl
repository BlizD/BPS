Функция УсловиеВыполнено(ДопПараметры,Отказ) Экспорт 
	Перем Результат;	
	
	пУсловиеВыполнено = Ложь;
	ОбъектБД = ДопПараметры.ОбъектБД;
	Условие = ДопПараметры.Условие;
	Если НЕ ЗначениеЗаполнено(Условие) Тогда
		пУсловиеВыполнено = Истина;
		Возврат пУсловиеВыполнено;
	Конецесли;
	
	
	Если Условие.ПроизвольноеУсловие Тогда		
		Выполнить(Условие.ТекстПроизвольногоУсловия);
		пУсловиеВыполнено = Результат;
	Иначе
		пНастройкиКомпоновщика = Условие.НастройкиКомпоновщикаХранилище.Получить();
		МакетКомпоновкиДанных = ПолучитьМакетКомпоновкиДанных(Условие.Владелец,пНастройкиКомпоновщика);
		пТекстЗапроса = МакетКомпоновкиДанных.НаборыДанных.НаборДанных1.Запрос;
		Запрос = Новый Запрос();
		Запрос.Текст = пТекстЗапроса;
		Запрос.УстановитьПараметр("Ссылка",ОбъектБД);
		Для каждого Параметр из МакетКомпоновкиДанных.ЗначенияПараметров цикл
			Запрос.УстановитьПараметр(Параметр.Имя,Параметр.Значение);	
		Конеццикла; 
		РезультатЗапроса = Запрос.Выполнить();
		пУсловиеВыполнено = НЕ РезультатЗапроса.Пустой();		
	Конецесли;	
	
	Если пУсловиеВыполнено = Истина И Условие.ВыборПоПодразделениям Тогда
		ПодразделениеАдресации = ОбъектБД[Условие.РеквизитПодразделение];
		ПользовательАдресации = РегистрыСведений.бпсРегистрАдресации.ПолучитьПользователяАдресации(ДопПараметры.РольАдресации,ПодразделениеАдресации);
		Если ПользовательАдресации = Неопределено Тогда
			пУсловиеВыполнено = Ложь;
			Если НЕ ДопПараметры.ПропускатьЕслиНеЗаданАдресат Тогда
				Отказ = Истина;
				ВызватьИсключение("Не найден адресат для роли " + СокрЛП(ДопПараметры.РольАдресации) + " и подразделения адресации " + СокрЛП(ПодразделениеАдресации));
			КонецЕсли;
		Иначе
			пУсловиеВыполнено = Истина;
		КонецЕсли;
	Конецесли;	
	
	Возврат пУсловиеВыполнено;
КонецФункции //УсловиеВыполнено(ДопПараметры,Отказ)

Функция СформироватьТекстЗапроса(ТипОбъектаБД, НастройкиКомпоновщика) Экспорт 
	МакетКомпоновкиДанных = ПолучитьМакетКомпоновкиДанных(ТипОбъектаБД,НастройкиКомпоновщика);	
	пТекстЗапроса = МакетКомпоновкиДанных.НаборыДанных.НаборДанных1.Запрос;
	Возврат пТекстЗапроса;
КонецФункции //СформироватьТекстЗапроса()

Функция ПолучитьМакетКомпоновкиДанных(ТипОбъектаБД,НастройкиКомпоновщика) Экспорт 
	СхемаКомпоновкиДанныхКонсоли = ПолучитьМакетСКДДляТипаОбъектаБД(ТипОбъектаБД);
	
	Группировка = НастройкиКомпоновщика.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	Группировка.Использование = Истина;
	ПолеГруппировки = Группировка.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
	ПолеГруппировки.Использование = Истина;
	ВыбранноеПоле           = Группировка.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
	ВыбранноеПоле.Поле      = Новый ПолеКомпоновкиДанных("Ссылка");
	ВыбранноеПоле.Заголовок = "Ссылка";

	ИсполняемыеНастройки = НастройкиКомпоновщика;
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанныхКонсоли, ИсполняемыеНастройки);
	Возврат МакетКомпоновкиДанных;
КонецФункции //ПолучитьМакетКомпоновкиДанных()

Функция ПолучитьМакетСКДДляТипаОбъектаБД(ТипОбъектаБД) Экспорт 
	Если НЕ ЗначениеЗаполнено(ТипОбъектаБД) Тогда
		Возврат Неопределено;
	Конецесли;
	МакетСКД = Новый СхемаКомпоновкиДанных; 
    
    // определим источник данных для схемы 
    Источник = МакетСКД.ИсточникиДанных.Добавить();
    Источник.Имя = "ЛокальнаяБаза";
    Источник.СтрокаСоединения = "";
    Источник.ТипИсточникаДанных = "Local"; 
    
    НаборДанных = МакетСКД.НаборыДанных.Добавить(Тип("НаборДанныхЗапросСхемыКомпоновкиДанных"));
    НаборДанных.Имя = "НаборДанных1";
    НаборДанных.ИсточникДанных = "ЛокальнаяБаза";
    НаборДанных.АвтоЗаполнениеДоступныхПолей = Истина;	
	
	пОбъектБДИмяМетаданных = ТипОбъектаБД.ОбъектБДИмяМетаданных;
	пМенеджерОбъекта = ТипОбъектаБД.МенеджерОбъекта;
	пМенеджерОбъектаДляЗапроса = ТипОбъектаБД.МенеджерОбъектаДляЗапроса;
	
	пТекстЗапроса = "ВЫБРАТЬ
	|	ЗапросКРеквизитамОбъекта.Ссылка КАК Ссылка";
	МетаданныеРеквизиты = Метаданные[пМенеджерОбъекта][пОбъектБДИмяМетаданных].Реквизиты;
	Для каждого пРеквизит из МетаданныеРеквизиты цикл
		пТекстЗапроса = пТекстЗапроса + "
		|	,"+пРеквизит.Имя + " КАК "+пРеквизит.Имя + "";			
	Конеццикла;
	пТекстЗапроса = пТекстЗапроса + "
	|ИЗ
	|	"+пМенеджерОбъектаДляЗапроса+"."+пОбъектБДИмяМетаданных+" КАК ЗапросКРеквизитамОбъекта
	|ГДЕ
	|	ЗапросКРеквизитамОбъекта.Ссылка = &Ссылка	
	|";
	НаборДанных.Запрос = (пТекстЗапроса);
    
    // добавим поля
    НаборДанных.АвтоЗаполнениеДоступныхПолей = Истина;
	
	Возврат МакетСКД;
КонецФункции //ПолучитьСКДДляОбъектаБД()