Процедура УстановитьСтатусыОбъектов (ДопПараметры,Отказ=Ложь) Экспорт
	
	ДатаИзмененияСтатуса = ДопПараметры.ДатаИзмененияСтатуса;
	
	РезультатФункции = ПолучитьТЗДокументыРегистрации(ДопПараметры);
	
    ТЗДокументыРегистрации = РезультатФункции.ТЗДокументыРегистрации;
	ТЗОбъектыБД = РезультатФункции.ТЗОбъектыБД;
	Если ТЗОбъектыБД = Неопределено Тогда
		Возврат;
	Конецесли;
	
	Если ТЗОбъектыБД.Количество() = 0 Тогда
		ВызватьИсключение "Ошибка! нет строк в ТЗОбъектыБД для установки нового статуса";
	Конецесли;	
	
	пСтатус = Неопределено;
	пКомментарий = "";
	Если ДопПараметры.Свойство("Статус") 
		И ТипЗнч(ДопПараметры.Статус) = Тип("СправочникСсылка.бпсСтатусыОбъектов") Тогда
		пСтатус = ДопПараметры.Статус;
	Конецесли;
	Если ДопПараметры.Свойство("Комментарий") Тогда
		пКомментарий = ДопПараметры.Комментарий; 		
	Конецесли;
	
	пОснование = Неопределено;
	Если ДопПараметры.Свойство("Основание") Тогда
		пОснование = ДопПараметры.Основание; 		
	Конецесли;
	
	пПредметСогласования = ДопПараметры.ПредметСогласования; 		
	
	пПользователь = ПараметрыСеанса.ТекущийПользователь;
	
	Для каждого СтрокаТЗОбъектыБД из ТЗОбъектыБД цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТЗОбъектыБД.ОбъектБД) Тогда
			ВызватьИсключение "Ошибка! не указан ОбъектБД которму необходимо установить статус";
		Конецесли;
		Если ЗначениеЗаполнено(СтрокаТЗОбъектыБД.Статус) Тогда
			пСтатус = СтрокаТЗОбъектыБД.Статус;
		Конецесли;
		Если НЕ ЗначениеЗаполнено(пСтатус) Тогда
			ВызватьИсключение "Ошибка! не указан какой статус ОбъектБД необходимо установить";			
		Конецесли;		
		
		Если ТЗДокументыРегистрации = Неопределено Тогда
			ДокОбъект = Документы.бпсРегистрацияСтатусаОбъекта.СоздатьДокумент();
		Иначе
			ПараметрыОтбора=Новый Структура();
			ПараметрыОтбора.Вставить("ОбъектБД",СтрокаТЗОбъектыБД.ОбъектБД);
			ПараметрыОтбора.Вставить("Статус",пСтатус);
			Если ЗначениеЗаполнено(пОснование) Тогда
				ПараметрыОтбора.Вставить("Основание",пОснование);
			Конецесли;
			НайденныеСтроки = ТЗДокументыРегистрации.НайтиСтроки(ПараметрыОтбора);
			Если НайденныеСтроки.Количество()>0 Тогда
				ДокОбъект = НайденныеСтроки[0].Ссылка.ПолучитьОбъект();
			Иначе
				ДокОбъект = Документы.бпсРегистрацияСтатусаОбъекта.СоздатьДокумент(); 
			Конецесли;
		Конецесли;
		
		ДокОбъект.Дата = ДатаИзмененияСтатуса;
		ДокОбъект.Ответственный = пПользователь;
		ДокОбъект.ОбъектБД = СтрокаТЗОбъектыБД.ОбъектБД;
		ДокОбъект.ПредметСогласования = СтрокаТЗОбъектыБД.ПредметСогласования;
		ДокОбъект.Статус = пСтатус;
		ДокОбъект.Комментарий = пКомментарий;
		ДокОбъект.Основание = пОснование;
		
		Попытка
			ДокОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ВызватьИсключение "Ошибка! не удалось установить статус ["+пСтатус+"], т.к. не удалось провести "+ДокОбъект+"  ОписаниеОшибки"+ОписаниеОшибки();
		КонецПопытки;
	Конеццикла;
КонецПроцедуры //УстановитьСтатусыДокументов(ДопПараметры)

Функция ПолучитьТЗДокументыРегистрации(ДопПараметры) Экспорт 
	РезультатФункции = Новый Структура();
	РезультатФункции.Вставить("ТЗДокументыРегистрации",Неопределено);
	РезультатФункции.Вставить("ТЗОбъектыБД",Неопределено);
	
	ТЗОбъектыБД = Неопределено;
	Если ДопПараметры.Свойство("ТЗОбъектыБД") Тогда
		ТЗОбъектыБД = ДопПараметры.ТЗОбъектыБД;
	Конецесли;
	Если ТЗОбъектыБД = Неопределено
		ИЛИ ТипЗнч(ТЗОбъектыБД) <> Тип("ТаблицаЗначений")
		ИЛИ ТЗОбъектыБД.Количество()= 0 Тогда
		Возврат РезультатФункции;
	Конецесли;
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ТЗОбъектыБД.ОбъектБД,
	|	ТЗОбъектыБД.ПредметСогласования,
	|	ТЗОбъектыБД.Статус
	|ПОМЕСТИТЬ ТЗОбъектыБД
	|ИЗ
	|	&ТЗОбъектыБД КАК ТЗОбъектыБД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	бпсРегистрацияСтатусаОбъекта.Ссылка,
	|	бпсРегистрацияСтатусаОбъекта.ОбъектБД,
	|	бпсРегистрацияСтатусаОбъекта.ПредметСогласования,
	|	бпсРегистрацияСтатусаОбъекта.Основание КАК Основание,
	|	бпсРегистрацияСтатусаОбъекта.Статус
	|ИЗ
	|	Документ.бпсРегистрацияСтатусаОбъекта КАК бпсРегистрацияСтатусаОбъекта
	|ГДЕ
	|	бпсРегистрацияСтатусаОбъекта.Проведен
	|	И (бпсРегистрацияСтатусаОбъекта.ОбъектБД, бпсРегистрацияСтатусаОбъекта.ПредметСогласования, бпсРегистрацияСтатусаОбъекта.Статус) В
	|			(ВЫБРАТЬ
	|				ТЗОбъектыБД.ОбъектБД,
	|				ТЗОбъектыБД.ПредметСогласования,
	|				ТЗОбъектыБД.Статус
	|			ИЗ
	|				ТЗОбъектыБД)
	|	И бпсРегистрацияСтатусаОбъекта.Дата = &ДатаИзмененияСтатуса");

	ДатаИзмененияСтатуса = ДопПараметры.ДатаИзмененияСтатуса;
	Запрос.УстановитьПараметр("ТЗОбъектыБД", ТЗОбъектыБД);
	Запрос.УстановитьПараметр("ДатаИзмененияСтатуса", ДатаИзмененияСтатуса);

	ТЗДокументыРегистрации = Запрос.Выполнить().Выгрузить();	
	РезультатФункции.Вставить("ТЗДокументыРегистрации",ТЗДокументыРегистрации);
	РезультатФункции.Вставить("ТЗОбъектыБД",ТЗОбъектыБД);
	Возврат РезультатФункции;
КонецФункции //ПолучитьТЗДокументыРегистрации(ДопПараметры)
